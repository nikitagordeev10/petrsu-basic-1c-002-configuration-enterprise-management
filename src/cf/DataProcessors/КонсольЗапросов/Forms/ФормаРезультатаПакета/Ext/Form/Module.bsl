
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// На сервере

&НаСервере
Процедура ПоказатьРезультат()
	
	Попытка
		МассивРезультатов  = ПолучитьИзВременногоХранилища(Параметры.АдресМассиваРезультатов);
		дзРезультатЗапроса = МассивРезультатов[Параметры.ИндексПакета].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ПодготовитьКоллекцию(дзРезультатЗапроса.Колонки);
		ЗначениеВРеквизитФормы(дзРезультатЗапроса, "РезультатПакета");
	Исключение
		Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), СтатусСообщения.Важное);
	КонецПопытки;
	
КонецПроцедуры // ВыполнитьЗапросНаСервере()

&НаСервере
Процедура ПодготовитьКоллекцию(КоллекцияКолонок)
	
	УдаляемыеРеквизиты = Новый Массив;
	УдаляемыеЭлементы  = Новый Массив;
	Для Каждого Элемент Из Элементы.РезультатПакета.ПодчиненныеЭлементы Цикл 
		УдаляемыеРеквизиты.Добавить(Элемент.ПутьКДанным);
		УдаляемыеЭлементы.Добавить(Элементы[Элемент.Имя]);
	КонецЦикла;
	
	Для Каждого Элемент Из УдаляемыеЭлементы Цикл 
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого Колонка Из КоллекцияКолонок Цикл 
		Если Колонка.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) Тогда 
			Продолжить;
		КонецЕсли;
		
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "РезультатПакета", Колонка.Заголовок));
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Для Каждого Колонка Из КоллекцияКолонок Цикл 
		Если Колонка.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) Тогда 
			Продолжить;
		КонецЕсли;
		
		НовыйЭлемент = Элементы.Добавить("РезультатПакета" + Колонка.Имя, Тип("ПолеФормы"), Элементы.РезультатПакета);
		НовыйЭлемент.ПутьКДанным    = "РезультатПакета." + Колонка.Имя;
		НовыйЭлемент.Вид            = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ТолькоПросмотр = Истина;
	КонецЦикла;
	
КонецПроцедуры // ПодготовитьКоллекцию()

// На сервере без контекста

// На клиенте

// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчики событий элементов формы

// Обработчики событий формы

&НаКлиенте
Процедура РезультатПакетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.РезультатПакета.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда 
		ОткрытьЗначение(ТекущиеДанные[СтрЗаменить(Поле.Имя, "РезультатПакета", "")]);
	КонецЕсли;
	
КонецПроцедуры // РезультатПакетаВыбор()

// На сервере

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказатьРезультат();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = Параметры.ИмяТаблицы;
	
	Если Параметры.Отображение = 0 Тогда 
		Элементы.РезультатПакета.Отображение = ОтображениеТаблицы.Список;
	ИначеЕсли Параметры.Отображение = 1 Тогда 
		Элементы.РезультатПакета.Отображение = ОтображениеТаблицы.ИерархическийСписок;
	Иначе
		Элементы.РезультатПакета.Отображение = ОтображениеТаблицы.Дерево;
	КонецЕсли;
	
	Элементы.Комментарий.Заголовок = "Количество строк результата: " + Формат(РезультатПакета.ПолучитьЭлементы().Количество(), "ЧЦ=15; ЧДЦ=0; ЧРГ=' '; ЧН=0");
	
КонецПроцедуры // ПриОткрытии()

// На клиенте

// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ДЕЙСТВИЙ

